apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "by.vshkl.bashq"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.0"
        resConfigs "en", "ru"
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix '-DEBUG'
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            splits {
                abi {
                    enable true
                    reset()
                    include "x86", "x86_64", "arm64-v8a", "armeabi", "armeabi-v7a"
                }
            }
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

ext.abiCodes = ['x86':1, 'x86_64':2, 'arm64-v8a':3, 'armeabi':4, 'armeabi-v7a':5]

import com.android.build.OutputFile

android.applicationVariants.all { variant ->

    variant.outputs.each { output ->
        def baseAbiVersionCode = project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))
        if (baseAbiVersionCode != null) {
            output.versionCodeOverride = baseAbiVersionCode * 1000 + variant.versionCode
        }
    }
}

dependencies {
    // Tests
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    // Support libraries
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:design:25.1.1'
    compile 'com.android.support:recyclerview-v7:25.1.1'
    compile 'com.android.support:cardview-v7:25.1.1'
    // Services
    compile 'com.google.firebase:firebase-core:10.0.1'
    compile 'com.google.firebase:firebase-crash:10.0.1'
    // Dependency injection
    compile 'com.google.dagger:dagger:2.0'
    apt 'com.google.dagger:dagger-compiler:2.0'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    // View injection
    compile 'com.jakewharton:butterknife:8.5.1'
    apt 'com.jakewharton:butterknife-compiler:8.5.1'
    // HTML extracting
    compile 'org.jsoup:jsoup:1.10.1'
    // Reactive
    compile 'io.reactivex.rxjava2:rxjava:2.0.4'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    // Networking
    compile 'com.squareup.okhttp3:okhttp:3.5.0'
    // Navigation drawer
    compile('com.mikepenz:materialdrawer:5.8.1@aar') {
        transitive = true
    }
    // Custom typeface
    compile 'uk.co.chrisjenx:calligraphy:2.2.0'
    // Date picker
    compile 'com.jzxiang.pickerview:TimePickerDialog:1.0.1'
    // Floating action button menu
    compile 'com.github.clans:fab:1.6.4'
    // Fancy spark button
    compile 'hanks.xyz:smallbang-library:0.1.2'
    // Image loading
    compile 'com.facebook.fresco:fresco:1.0.1'
    compile 'com.github.stfalcon:frescoimageviewer:0.4.1'
    // Database
    apt "com.github.Raizlabs.DBFlow:dbflow-processor:4.0.0-beta5"
    compile 'com.github.Raizlabs.DBFlow:dbflow-core:4.0.0-beta5'
    compile 'com.github.Raizlabs.DBFlow:dbflow:4.0.0-beta5'
    // MVP module
    compile project(':mvp')
    // LeakCanary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
}

repositories {
    mavenCentral()
}

apply plugin: 'com.google.gms.google-services'
